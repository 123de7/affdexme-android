apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId "com.affectiva.affdexme"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 15
        versionName "2.1.0"
        setProperty("archivesBaseName", "AffdexMe-$versionName-$versionCode")
    }
    buildTypes {
        debug {
            resValue 'string', 'app_name', 'AffdexMeDbg'
            applicationIdSuffix ".debug"
        }
        release {
            resValue 'string', 'app_name', 'AffdexMe'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['jniLibs']
            jni.srcDirs = [] //disable automatic ndk-build
        }
    }
}

dependencies {
    //include the Affdex SDK jars
    compile files('libs/Affdex-sdk.jar')
    compile files('libs/Affdex-sdk-javadoc.jar')

    //include dependencies
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'javax.inject:javax.inject:1'

    //although the use of the CameraDetector class in this project does not require it, you may have to include
    //the following dependencies if you use other features of the Affdex SDK
//    compile 'com.google.code.gson:gson:2.4'
//    compile 'com.android.support:support-v13:23.1.1'
//    compile 'com.google.android.gms:play-services:8.4.0'
//    compile 'com.google.android.gms:play-services-ads:8.4.0'
//    compile 'com.google.android.gms:play-services-identity:8.4.0'
//    compile 'com.google.android.gms:play-services-gcm:8.4.0'
//    compile files('libs/flurry-analytics-5.0.0.jar')
}

// build a signed release apk only if the environment is configured
// with the properties that let us access the keystore.  you can put
// them into ~/.gradle/gradle.properties
if (project.hasProperty('releaseKeystoreFile')) {
    android.signingConfigs {
        release {
            storeFile file(releaseKeystoreFile)
            storePassword releaseKeystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
        }
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}
