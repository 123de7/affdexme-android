apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId "com.affectiva.affdexme"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 14
        versionName "2.0.0"
        setProperty("archivesBaseName", "AffdexMe-$versionName-$versionCode")
    }
    buildTypes {
        debug {
            resValue 'string', 'app_name', 'AffdexMeDbg'
            applicationIdSuffix ".debug"
        }
        release {
            resValue 'string', 'app_name', 'AffdexMe'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['native-libs']
            jni.srcDirs = [] //disable automatic ndk-build
        }
    }
}

dependencies {
	//gson is necessary for the license to be parsed
    compile 'com.google.code.gson:gson:2.3'
	
	//include the Affdex SDK jars
    compile files('libs/Affdex-sdk.jar')
    compile files('libs/Affdex-sdk-javadoc.jar')
    compile files('libs/dagger-1.2.2.jar')
    compile files('libs/flurry-analytics-4.1.0.jar')
    compile files('libs/javax.inject-1.jar')
	
	//although the use of the CameraDetector class in this project does not require it, you may have to include
	//the following dependencies if you use other features of the Affdex SDK
	//compile 'com.android.support:support-v4:22.2.0'
    //compile 'com.google.android.gms:play-services:7.5.0'
	
}

// build a signed release apk only if the environment is configured
// with the properties that let us access the keystore.  you can put
// them into ~/.gradle/gradle.properties
if (project.hasProperty('releaseKeystoreFile')) {
    android.signingConfigs {
        release {
            storeFile file(releaseKeystoreFile)
            storePassword releaseKeystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
        }
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}
